
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьТекущееРасположение();
	ПолучитьСоответствияИз7();
	ПолучитьАктуальность();
	
КонецПроцедуры

&НаКлиенте
Процедура КодБСВМММПриИзменении(Элемент)
	
	КодБСВМММПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АктуальнаПриИзменении(Элемент)
	
	УстановитьАктуальностьНаСервере();	
	
КонецПроцедуры


&НаКлиенте
Процедура ДекорацияИсторияАктуальностиНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.АктуальностьОрганизаций.ФормаСписка", 
		Новый Структура("Отбор", 
			Новый Структура("Организация", Объект.Ссылка)
		)
	);
		
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьТекущееРасположение()
	
	Расположение = РегистрыСведений.РасположениеФирм;
	ТекущееРасположение = Расположение.ПолучитьПоследнее(ТекущаяДатаСеанса(), Новый Структура("Организация", Объект.Ссылка));
	
	Сервер = ТекущееРасположение.Сервер;
	База = ТекущееРасположение.ИмяБазы;
	Логин = ТекущееРасположение.Логин;
	Пароль = ТекущееРасположение.Пароль;
	
КонецПроцедуры	

&НаСервере
Процедура ПолучитьСоответствияИз7()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СвязьОрганизаций8ИСчетов7.Счет7 КАК Счет7
	               |ИЗ
	               |	РегистрСведений.СвязьОрганизаций8ИСчетов7 КАК СвязьОрганизаций8ИСчетов7
	               |ГДЕ
	               |	СвязьОрганизаций8ИСчетов7.Организация8 = &Организация";
	Запрос.УстановитьПараметр("Организация", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.КодБСВМММ = Выборка.Счет7;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура КодБСВМММПриИзмененииНаСервере()
	
	НаборЗаписей = РегистрыСведений.СвязьОрганизаций8ИСчетов7.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация8.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Период.Установить(НачалоМесяца(ТекущаяДатаСеанса()));
	НаборЗаписей.Очистить();
	Запись = НаборЗаписей.Добавить();
	Запись.Период = НачалоМесяца(ТекущаяДатаСеанса());
	Запись.Организация8 = Объект.Ссылка;
	Запись.Счет7 = Объект.КодБСВМММ;
	НаборЗаписей.Записать();
	
КонецПроцедуры	

&НаСервере
Процедура ПолучитьАктуальность()
	
	РегистрыСведений.АктуальностьОрганизаций.ПолучитьПоследнее(ТекущаяДатаСеанса(), 
		Новый Структура("Организация", Объект.Ссылка)
	).Свойство("Активна", Актуальна);	
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьАктуальностьНаСервере()
	
	Менеджер = РегистрыСведений.АктуальностьОрганизаций.СоздатьМенеджерЗаписи();
	Менеджер.Период = НачалоМесяца(ТекущаяДатаСеанса());
	Менеджер.Организация = Объект.Ссылка;
	Менеджер.Активна = Актуальна;
	Менеджер.Записать();
	
КонецПроцедуры	

#КонецОбласти
