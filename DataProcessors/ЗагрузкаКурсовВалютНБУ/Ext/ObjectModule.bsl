
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	НазваОбробки = НазваОбробки();
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.1.3.1");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = НазваОбробки;
	ПараметрыРегистрации.Версия = ПоточнаВерсіяОбробки();
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Информация = НСтр("ru = 'Обробка завантаження курсів валют (НБУ)'; uk = 'Обробка завантаження курсів валют (НБУ)'");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НазваОбробки;
	Команда.Идентификатор = ІмяОбробки();
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	Команда.ПоказыватьОповещение = Истина;
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НазваОбробки + ": " + НСтр("ru = 'завантаження курсів усіх валют'; uk = 'завантаження курсів усіх валют'");
	Команда.Идентификатор = "ЗавантаженняКурсівУсіхВалют";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	Если ИдентификаторКоманды = "ЗавантаженняКурсівУсіхВалют" Тогда
		ЗавантажитиКурсиУсіхВалют();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Функції

Функция ПоточнаВерсіяОбробки() Экспорт
	Возврат "1.5";
КонецФункции

Функция ІмяОбробки() Экспорт
	Возврат ЭтотОбъект.Метаданные().Имя;
КонецФункции

Функция НазваОбробки() Экспорт
	Возврат ЭтотОбъект.Метаданные().Синоним;
КонецФункции

Процедура ЗавантажитиКурсиУсіхВалют() Экспорт
	
	ПоточнаДата = НачалоДня(ТекущаяДатаСеанса());
	
	Гривня = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запит = Новый Запрос;
	ТекстЗапиту =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Валюта.Наименование КАК ЛітернийКод,
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратність
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ПоточнаДата, Валюта <> &Гривня И НЕ Валюта.ПометкаУдаления) КАК КурсыВалютСрезПоследних";
	Запит.Параметры.Вставить("ПоточнаДата", ПоточнаДата);
	Запит.Параметры.Вставить("Гривня", Гривня);
	Запит.Текст = ТекстЗапиту;
	РезультатЗапиту = Запит.Выполнить();
	Вибірка = РезультатЗапиту.Выбрать();
	ЗапитиКурсівВалют = Новый Массив;
	Пока Вибірка.Следующий() Цикл
		Если Не ПустаяСтрока(Вибірка.ЛітернийКод) Тогда
			ПотДата = Макс(Вибірка.Период, '19960901');
			ЛітернийКод = СокрЛП(Вибірка.ЛітернийКод);
			Пока ПотДата < ПоточнаДата Цикл
				ПотДата = ПотДата + 60 * 60 * 24;
				ПараметриВалюти = Новый Структура;
				ПараметриВалюти.Вставить("ДатаКурсу", ПотДата);
				ПараметриВалюти.Вставить("Валюта", Вибірка.Валюта);
				ПараметриВалюти.Вставить("ЛітернийКод", ЛітернийКод);
				ПараметриВалюти.Вставить("Кратність", Вибірка.Кратність);
				ПараметриВалюти.Вставить("КурсНБУ", 0);
				ЗапитиКурсівВалют.Добавить(ПараметриВалюти);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	Если ЗапитиКурсівВалют.Количество() > 0 Тогда
		ЗапитДоСерверуНБУ(ЗапитиКурсівВалют);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапитДоСерверуНБУ(ЗапитиКурсівВалют)
	
	ВідповідіКурсівВалют = Новый Массив;
	
	СерверСервісу = "bank.gov.ua/NBUStatService";
	АдресаЗапиту = "/v1/statdirectory/exchange?";
	Проксі = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("https");
	HTTPЗєднання = Новый HTTPСоединение(СерверСервісу, , , , Проксі, , Новый ЗащищенноеСоединениеOpenSSL());
	
	Для Каждого ПараметриВалюти Из ЗапитиКурсівВалют Цикл
		
		HTTPЗапит = Новый HTTPЗапрос;
		HTTPЗапит.АдресРесурса = АдресаЗапиту + "valcode=" + ПараметриВалюти.ЛітернийКод + "&date=" + Формат(ПараметриВалюти.ДатаКурсу, "ДФ = yyyyMMdd") + "&json";
		
		
		КодСтану = 503;
		ЛчВідмов = 0;
		Пока КодСтану = 503 И ЛчВідмов < 10 Цикл
			
			Если ЛчВідмов > 0 Тогда
				ЗавершенняТаймАуту = ТекущаяДатаСеанса() + 1;
				Пока ТекущаяДатаСеанса() < ЗавершенняТаймАуту Цикл
				КонецЦикла;
			КонецЕсли;
			
			HTTPВідповідь = HTTPЗєднання.Получить(HTTPЗапит);
			
			КодСтану = HTTPВідповідь.КодСостояния;
			Если КодСтану = 503 Тогда
				ЛчВідмов = ЛчВідмов + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		РядокВідповіді = HTTPВідповідь.ПолучитьТелоКакСтроку();
		
		Если КодСтану = 200 Тогда
			
			ЧитанняJSON = Новый ЧтениеJSON;
			ЧитанняJSON.УстановитьСтроку(РядокВідповіді);
			Попытка
				Відповідь = ПрочитатьJSON(ЧитанняJSON);
				Если Відповідь.Количество() > 0 И Відповідь[0].Свойство("rate") Тогда
					ПараметриВалюти.КурсНБУ = Відповідь[0].rate;
					ВідповідіКурсівВалют.Добавить(ПараметриВалюти);
				КонецЕсли;
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Отримання курсів НБУ'; uk = 'Отримання курсів НБУ'"),
					УровеньЖурналаРегистрации.Ошибка, , , НСтр("ru = 'Відповідь сервера'; uk = 'Відповідь сервера'") + ": " + Символы.ПС + РядокВідповіді);
			КонецПопытки;
			
		Иначе
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Отримання курсів НБУ'; uk = 'Отримання курсів НБУ'"),
				УровеньЖурналаРегистрации.Ошибка, , , НСтр("ru = 'Відповідь сервера'; uk = 'Відповідь сервера'") + ": " + Символы.ПС + РядокВідповіді);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВідповідіКурсівВалют.Количество() > 0 Тогда
		ЗаписатиКурсиВалют(ВідповідіКурсівВалют);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатиКурсиВалют(ВідповідіКурсівВалют)
	
	Для Каждого ПараметриВалюти Из ВідповідіКурсівВалют Цикл
		
		Если ПараметриВалюти.Кратність <> 0 И ПараметриВалюти.КурсНБУ <> 0 Тогда
			
			МенеджерЗаписуКурсівВалют = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
			МенеджерЗаписуКурсівВалют.Валюта = ПараметриВалюти.Валюта;
			МенеджерЗаписуКурсівВалют.Период = ПараметриВалюти.ДатаКурсу;
			МенеджерЗаписуКурсівВалют.Кратность = ПараметриВалюти.Кратність;
			МенеджерЗаписуКурсівВалют.Курс = ПараметриВалюти.КурсНБУ * ПараметриВалюти.Кратність;
			Попытка
				МенеджерЗаписуКурсівВалют.Записать(Истина);
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Запис курсу НБУ'; uk = 'Запис курсу НБУ'"),
					УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
